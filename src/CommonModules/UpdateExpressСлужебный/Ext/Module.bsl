// This file is part of Update.Express.
// Copyright © 2016 Petro Bazeliuk.
// 
// Update.Express is free software: you can redistribute it and/or modify
// it under the terms of the GNU Lesser General Public License as 
// published by the Free Software Foundation, either version 3 
// of the License, or any later version.
// 
// Update.Express is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Lesser General Public License for more details.
// 
// You should have received a copy of the GNU Lesser General Public 
// License along with Update.Express. If not, see <http://www.gnu.org/licenses/>.

#Область ПрограммныйИнтерфейс

#Область ОперацииСоСтроками

// Конвертирует строку в число и возвразает Число или Неопределено.
// 
// Параметры:
//  Строка - Строка - конвертируемая строка.
//
// Возвращаемое значение:
//  Число, Неопределено - Число или Неопределено из строки.
//
Функция ПопытатьсяКонвертироватьСтрокуВЧисло(Строка) Экспорт
	
	Попытка
		Если ТипЗнч(Строка) = Тип("Строка") Тогда
			Возврат Число(Строка);
		КонецЕсли;
	Исключение		
	КонецПопытки;
	
	Возврат Неопределено;
	
КонецФункции // ПопытатьсяКонвертироватьСтрокуВЧисло() 

// Возвращает массив строк по указаному разделителю.
// 
// Параметры:
//  Строка 	  	- Строка - строка которую необходимо разделить.
//	Разделитель - Строка - строчный разделитель.
//
// Возвращаемое значение:
//  Массив - возвращаемый массив строк.
//
Функция РазделитьСтроку(Строка, Разделитель = ".") Экспорт
	
	МассивСтрок = Новый Массив;
    СтрокаСПереносами = СтрЗаменить(Строка, Разделитель, Символы.ПС);
    Для Инд = 1 По СтрДлина(СтрокаСПереносами) Цикл МассивСтрок.Добавить(СтрПолучитьСтроку(СтрокаСПереносами, Инд)); КонецЦикла;
    Возврат МассивСтрок; 
	
КонецФункции // РазделитьСтроку()

// Подставляет параметры в строку. Максимально возможное число параметров - 9.
// Параметры в строке задаются как %<номер параметра>. Нумерация параметров начинается с единицы.
//
// Параметры:
//  СтрокаПодстановки  – Строка – шаблон строки с параметрами (вхождениями вида "%ИмяПараметра").
//  Параметр<n>        - Строка - подставляемый параметр.
//
// Возвращаемое значение:
//  Строка – текстовая строка с подставленными параметрами.
//
// Пример:
//  ПодставитьПараметрыВСтроку(НСтр("ru='%1 пошел в %2'"), "Вася", "Зоопарк") = "Вася пошел в Зоопарк".
//
Функция ПодставитьПараметрыВСтроку(Знач СтрокаПодстановки,
	Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено) Экспорт
	
	ИспользоватьАльтернативныйАлгоритм = 
		Найти(Параметр1, "%")
		Или Найти(Параметр2, "%")
		Или Найти(Параметр3, "%")
		Или Найти(Параметр4, "%")
		Или Найти(Параметр5, "%")
		Или Найти(Параметр6, "%")
		Или Найти(Параметр7, "%")
		Или Найти(Параметр8, "%")
		Или Найти(Параметр9, "%");
		
	Если ИспользоватьАльтернативныйАлгоритм Тогда
		СтрокаПодстановки = ПодставитьПараметрыВСтрокуАльтернативныйАлгоритм(СтрокаПодстановки, Параметр1,
			Параметр2, Параметр3, Параметр4, Параметр5, Параметр6, Параметр7, Параметр8, Параметр9);
	Иначе
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%1", Параметр1);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%2", Параметр2);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%3", Параметр3);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%4", Параметр4);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%5", Параметр5);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%6", Параметр6);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%7", Параметр7);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%8", Параметр8);
		СтрокаПодстановки = СтрЗаменить(СтрокаПодстановки, "%9", Параметр9);
	КонецЕсли;
	
	Возврат СтрокаПодстановки;
КонецФункции

#КонецОбласти // ОперацииСоСтроками

#Область ОперацииXML

// Функция предназначена для сериализации значения в формат XML.
//
// Параметры:
//	Значение - Произвольный - значение, которое необходимо сериализировать.
//
// Возвращаемое значение:
//	Строка - строка, которая содержит значение в формате XML.	
//
Функция ПолучитьXML(Значение) Экспорт
	
	Запись = Новый ЗаписьXML();
	Запись.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(Запись, Значение);
	Возврат Запись.Закрыть();
	
КонецФункции // ПолучитьXML()

#КонецОбласти // ОперацииXML

#КонецОбласти // ПрограммныйИнтерфейс

#Область СлужебныйПрограммныйИнтерфейс 

// Функция возвращает менеджер объекта конфигурации для переданного типа XML.
//
// Параметры:
//	ИмяТипаXML - Строка - имя типа XML для которого необходимо получить менеджер.
//	
// Возвращаемое значение:
//	Неопределено, 
//	СправочникиМенеджер,
//	ДокументыМенеджер,
//	ПеречисленияМенеджер,
//	ПланыОбменаМенеджер,
//	ПланыВидовХарактеристикМенеджер,
//	ПланыСчетовМенеджер,
//  ПланыВидовРасчетаМенеджер,
//	ЗадачиМенеджер,
//	БизнесПроцессыМенеджер.
//
Функция ПолучитьМенеджерОбъектаПоИмениТипаXML(ИмяТипаXML) Экспорт
	
	ТипОбъекта = Лев(ИмяТипаXML, Найти(ИмяТипаXML, ".") - 1);
	Если ПустаяСтрока(ТипОбъекта) Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ВТипОбъекта = ВРег(ТипОбъекта);
	Если ВТипОбъекта = "CATALOGREF" Тогда
		
		Возврат Справочники;
		
	ИначеЕсли ВТипОбъекта = "DOCUMENTREF" Тогда 
		
		Возврат Документы;
		
	ИначеЕсли ВТипОбъекта = "ENUMREF" Тогда 
		
		Возврат Перечисления;
		
	ИначеЕсли ВТипОбъекта = "EXCHANGEPLANREF" Тогда 
		
		Возврат ПланыОбмена;
		
	ИначеЕсли ВТипОбъекта = "CHARTOFCHARACTERISTICTYPESREF" Тогда 
		
		Возврат ПланыВидовХарактеристик;
		
	ИначеЕсли ВТипОбъекта = "CHARTOFACCOUNTSREF" Тогда 
		
		Возврат ПланыСчетов;
		
	ИначеЕсли ВТипОбъекта = "CHARTOFCALCULATIONTYPESREF" Тогда 
		
		Возврат ПланыВидовРасчета;
		
	ИначеЕсли ВТипОбъекта = "TASKREF" Тогда 
		
		Возврат Задачи;
		
	ИначеЕсли ВТипОбъекта = "BUSINESSPROCESSREF" Тогда 
		
		Возврат БизнесПроцессы;

	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции // ПолучитьМенеджерОбъектаПоИмениТипаXML()

// Функция проверяет может ли значение переменной интерпретироваться как ДополнительныеПараметры.
//
// Параметры:
//	Значение - Произвольный - значение которое необходимо проверить. 
//
// Возвращаемое значение:
//	Булево - Истина - указанное значение может интерпретироваться как ДополнительныеПараметры; Ложь - в противном случае.	
//
Функция ЭтоДополнительныеПараметры(Значение) Экспорт
	
	Если Значение <> Неопределено Тогда
		Если ТипЗнч(Значение) = Тип("Структура") Тогда
			Возврат Истина;	
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции // ЭтоДополнительныеПараметры()

#КонецОбласти // СлужебныйПрограммныйИнтерфейс

#Область СервисныеПроцедурыИФункции

#Область ОперацииСоСтроками

// Вставляет параметры в строку, учитывая, что в параметрах могут использоваться подстановочные слова %1, %2 и т.д.
Функция ПодставитьПараметрыВСтрокуАльтернативныйАлгоритм(Знач СтрокаПодстановки,
	Знач Параметр1, Знач Параметр2 = Неопределено, Знач Параметр3 = Неопределено,
	Знач Параметр4 = Неопределено, Знач Параметр5 = Неопределено, Знач Параметр6 = Неопределено,
	Знач Параметр7 = Неопределено, Знач Параметр8 = Неопределено, Знач Параметр9 = Неопределено)
	
	Результат = "";
	Позиция = Найти(СтрокаПодстановки, "%");
	Пока Позиция > 0 Цикл 
		Результат = Результат + Лев(СтрокаПодстановки, Позиция - 1);
		СимволПослеПроцента = Сред(СтрокаПодстановки, Позиция + 1, 1);
		ПодставляемыйПараметр = "";
		Если СимволПослеПроцента = "1" Тогда
			ПодставляемыйПараметр =  Параметр1;
		ИначеЕсли СимволПослеПроцента = "2" Тогда
			ПодставляемыйПараметр =  Параметр2;
		ИначеЕсли СимволПослеПроцента = "3" Тогда
			ПодставляемыйПараметр =  Параметр3;
		ИначеЕсли СимволПослеПроцента = "4" Тогда
			ПодставляемыйПараметр =  Параметр4;
		ИначеЕсли СимволПослеПроцента = "5" Тогда
			ПодставляемыйПараметр =  Параметр5;
		ИначеЕсли СимволПослеПроцента = "6" Тогда
			ПодставляемыйПараметр =  Параметр6;
		ИначеЕсли СимволПослеПроцента = "7" Тогда
			ПодставляемыйПараметр =  Параметр7
		ИначеЕсли СимволПослеПроцента = "8" Тогда
			ПодставляемыйПараметр =  Параметр8;
		ИначеЕсли СимволПослеПроцента = "9" Тогда
			ПодставляемыйПараметр =  Параметр9;
		КонецЕсли;
		Если ПодставляемыйПараметр = "" Тогда
			Результат = Результат + "%";
			СтрокаПодстановки = Сред(СтрокаПодстановки, Позиция + 1);
		Иначе
			Результат = Результат + ПодставляемыйПараметр;
			СтрокаПодстановки = Сред(СтрокаПодстановки, Позиция + 2);
		КонецЕсли;
		Позиция = Найти(СтрокаПодстановки, "%");
	КонецЦикла;
	Результат = Результат + СтрокаПодстановки;
	
	Возврат Результат;
КонецФункции

#КонецОбласти // ОперацииСоСтроками

#КонецОбласти // СервисныеПроцедурыИФункции